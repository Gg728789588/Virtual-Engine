#include "Include/Sampler.cginc"
cbuffer CullBuffer : register(b0)
{
    float4 planes[6];
    //Align
    float4 _FrustumMinPoint;
    float4 _FrustumMaxPoint;
    uint _Count;
};
cbuffer MoveTheWorld : register(b1)
{
    float3 _MoveDir;
    uint _MoveCount;
}
struct MultiDrawCommand
{
    uint IndexCountPerInstance;
    uint InstanceCount;
    uint StartIndexLocation;
    int BaseVertexLocation;
    uint StartInstanceLocation;
};
struct ObjectData
{
    float3 worldPos;			
};
StructuredBuffer<ObjectData> _InputDataBuffer : register(t0);
StructuredBuffer<uint2> _RemoveBuffer : register(t1);
RWStructuredBuffer<MultiDrawCommand> _OutputBuffer : register(u0);
RWStructuredBuffer<ObjectData> _OutputDataBuffer : register(u1);


float PlaneTest(float3 worldPos){
    const float3 localPosition = 0;
    const float3 extent = 0.5;
    float3 position = worldPos + localPosition;
    float3 worldMin = position;
    float3 worldMax = position;
    uint i;
    
    const float3 offsetRate[8] = {
        float3(1, 1, 1),
        float3(1, 1, -1),
        float3(1, -1, 1),
        float3(1, -1, -1),
        float3(-1, 1, 1),
        float3(-1, 1, -1),
        float3(-1, -1, 1),
        float3(-1, -1, -1)
    };
    
    [loop]
    for(i = 0; i < 8; ++i)
    {
        float3 currPos = worldPos + (localPosition + extent * offsetRate[i]);
        worldMin = min(worldMin, currPos);
        worldMax = max(worldMax, currPos);
    }
    float outOfRange = dot(_FrustumMinPoint.xyz > worldMax, 1) + dot(_FrustumMaxPoint.xyz < worldMin, 1);
    if(outOfRange > 0.5) return 0;
    [loop]
    for(i = 0; i < 6; ++i)
    {
        float4 plane = planes[i];
        float3 absNormal = abs(plane.xyz);
        if((dot(position, plane.xyz) - dot(absNormal, extent)) > -plane.w)
        {
            return 0;
        }
    }
    return 1;
}

[numthreads(64, 1, 1)]
void CSMain(uint id : SV_DISPATCHTHREADID)
{
    if(id >= _Count) return;
    uint count;
    ObjectData data = _InputDataBuffer[id];
    if(PlaneTest(data.worldPos) > 0.5)
    {
        InterlockedAdd(_OutputBuffer[0].InstanceCount, 1, count);
        _OutputDataBuffer[count] = data;
    }
}

[numthreads(1,1,1)]
void Clear()
{
    MultiDrawCommand clearedValue = (MultiDrawCommand)0;
    clearedValue.IndexCountPerInstance = 36;    //Cube Should have 36 vertex
    _OutputBuffer[0] = clearedValue;
}

[numthreads(64, 1, 1)]
void ExecuteRemoveCommand(uint id : SV_DISPATCHTHREADID)
{
    if(id >= _Count) return;
    uint2 moveCmd = _RemoveBuffer[id];
    _OutputDataBuffer[moveCmd.x] = _OutputDataBuffer[moveCmd.y];
}

[numthreads(64, 1, 1)]
void MoveTheWorld(uint id : SV_DISPATCHTHREADID)
{
    if(id >= _MoveCount) return;
    _OutputDataBuffer[id].worldPos += _MoveDir;
}